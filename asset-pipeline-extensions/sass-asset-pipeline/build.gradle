buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'idea'
group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
	mavenLocal()
    jcenter()
    mavenCentral()

}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    compileOnly "org.codehaus.groovy:groovy-all:$groovy"
	api project(':asset-pipeline-core')
    api "org.slf4j:slf4j-api:$slf4j"
    api 'io.bit3:jsass:5.10.4'
    testImplementation "org.codehaus.groovy:groovy-all:$groovy"
    testImplementation "org.spockframework:spock-core:$spockframework"

    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4j"
    testRuntimeOnly 'org.webjars:bootstrap:5.1.3'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'sass-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'sass-asset-pipeline'
                    description 'Provides SASS Compiling using jsass (libsass) support for the Asset-Pipeline library.'
                    url 'https://www.asset-pipeline.com'
                    scm {
                        url 'https://github.com/bertramdev/sass-asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/sass-asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/sass-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'gferon'
                            name 'Gabriel FÃ©ron'
                            email 'feron.gabriel@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}


task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

